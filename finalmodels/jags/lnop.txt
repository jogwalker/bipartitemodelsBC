      model 
{    
  for (i in 1:Nobs)  #for all observations run loop around 
  { 
    count[i] ~ dpois(mu[i])  #the count data with a Poission distribution (0, 1)
    mu[i] <- lambda[i] * use[host.sp[i], par.sp[i]]  #mean abundance = characteristic abundance of each insect sp. * use of the pl.sp. k that indiv. i belongs to 
    log(lambda[i]) <- beta[par.sp[i]]   + beta_pr*rain[ind[i]] + beta_t*treated[ind[i]] + beta_s*female[ind[i]] +  beta_j*juvenile[ind[i]] 
  }   
  
  for (n in 1: Nhost.sp) #loop around all plant species, calculate use, alpha and insect diversity
  {
    for (j in 1:Npar) #loop around all insect species
    {  
      use[n, j] ~ dbern(p[n, j])  # probability of use drawn from a Bernoullli distribution 
      logit(p[n, j]) <- alpha[n, j] # p is pi, logit is link function of Bernoulli distribution
      alpha[n, j] ~ dnorm(mn[1], prec[1]) # coefficient for use is drawn from normal distribution with mean and precision)
    }
    
    PD_host[n]<- sum (use[n, ]) #Insect diversity = sum of the uses for each plant species
  } 
  
  #Specify priors for missing data.
  for(i in 1:length(missing.ind.rain)) {
    rain[missing.ind.rain[i]] ~ dnorm(0,1)T(-1.738577, )
  }  

  for(i in 1:length(missing.ind.treat)) {
    treated[missing.ind.treat[i]] ~ dbern(0.5)
  } 

  for(i in 1:length(missing.ind.juv)) {
    juvenile[missing.ind.juv[i]] ~ dbern(prop)
  } 

  for(i in 1:length(missing.ind.fem)) {
    female[missing.ind.fem[i]] ~ dbern(0.5)
  } 


  
  # priors?   
  for (j in 1: Npar)  #loop around insect species, calculate charact abundance and host breadth
  {        
    beta[j] ~ dnorm(mn[2], prec[2]) # average abundance across all invertebrate sp. beta0 and a random effect coeff. beta1
    HB_invert[j] <- sum(use[, j]) #
  } 
  
  beta_pr ~ dnorm(0, 0.0001)
  beta_t ~ dnorm(0, 0.0001)
  beta_s ~ dnorm(0, 0.0001)
  beta_j ~ dnorm(0, 0.0001)
  
  for (k in 1:2) # 
  { 
    mn[k] ~ dnorm(0, .0001) 
    prec[k] <- pow(sd[k], -2)
    sd[k] ~ dunif(0, 100)
  }
} 