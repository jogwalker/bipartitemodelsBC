      model    
{
  for (i in 1:Nobs) { #for all observations run loop around 
    count[i] ~ dpois(mu[i])  #Poisson count data
    mu[i] <- lambda[i] * use[host.sp[i], par.sp[i]]  #mean abundance = abundance of each plant sp. * use of the pl.sp. k that indiv. i belongs to 
    log(lambda[i]) <- beta[par.gen[i]] # mean abundance of inv.sp. j on pl.ind. i
  }  
  
  for (n in 1: Nhost.sp) { #loop around all host species, calculate use, alpha and insect diversity
    
    for (j in 1:Npar) {  
      use[n, j] ~ dbern(p[n, j])  # probability of use drawn from a Bernoullli distribution 
      logit(p[n, j]) <- alpha[n, j] # refers to the identity list which names the category for each 
      alpha[n, j] ~ dnorm(mn[1], prec[1])
    }
    
    
    D_byhost[n]<- sum (use[n, ]) #sums up the use values for each host species
  } 
  
  for (j in 1: Npar) {  #loop around parasite species, calculate charact abundance and host breadth        
    #beta[j] ~ dnorm(mn[2], prec[2]) # average abundance across all invertebrate sp. beta0 and a random effect coeff. beta1
    HB_invert[j] <- sum(use[, j]) #how many hosts the parasite uses
  } 
  
  for (k in 1:Npar.gen) { # loop around parasite Genera
    beta[k] ~ dnorm(mn[2], prec[2]) 
  }
  
  for (k in 1:2) # 
  { 
    mn[k] ~ dnorm(0, .0001) # mean, precision
    prec[k] <- pow(sd[k], -2)
    sd[k] ~ dt(0, .016, 1)T(0, ) # a half-cauchy is apparently the t-distribution with 1 degree of freedom. 
    #sd[k] ~ dunif(0, 100)
  }
} 